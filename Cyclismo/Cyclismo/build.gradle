apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'

check.dependsOn 'findbugs'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0+'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile project(':Cyclismo:CyclismoLib')
    compile project(':aFileChooser')
    compile project(':jformica_android')
    compile project(':FluxUtils')
    compile project(':jformica_core')
    compile project(':JTurbo')
    androidTestCompile fileTree(include: '*.jar', dir: 'test_libs')
    androidTestCompile 'org.mockito:mockito-all:1.+'
    compile 'com.android.support:support-v4:22.0.0'

    // For MapForge
    compile 'com.caverock:androidsvg:1.2.2+'
    compile group: 'net.sf.kxml', name: 'kxml2', version:'2.3.0'
}

android {
    compileSdkVersion 19
    buildToolsVersion "22"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 16
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

task checkstyle(type: Checkstyle) {
    // FIXME: Check this in
    configFile file("${project.rootDir}/quality/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

findbugs {
    sourceSets = []
    ignoreFailures = true
}

task findbugs(type: FindBugs, dependsOn: assembleDebug) {

    description 'Run findbugs'
    group 'verification'

    classes = fileTree('build/intermediates/classes/debug/')
    classes.exclude '**/R.class'
    classes.exclude '**/R$*.class'

    source = fileTree('src')
    classpath = files()

    effort = 'max'

    excludeFilter = file("${project.rootDir}/quality/findbugs/exclude.xml")

    reports {
        xml.enabled = false
        html.enabled = true
    }
}

check.doLast {
    project.tasks.getByName("findbugs").execute()
}

